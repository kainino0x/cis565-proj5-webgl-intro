<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    void main(void)
    {
        vec2 cloudTC = v_Texcoord + vec2(u_time * -0.0001, 0);
        float cloudTrans = texture2D(u_CloudTrans, cloudTC).r;

        float b_center = texture2D(u_Bump, v_Texcoord).r;
        float b_right  = texture2D(u_Bump, v_Texcoord + vec2(1.0 / 1024.0, 0)).r;
        float b_top    = texture2D(u_Bump, v_Texcoord + vec2(0, 1.0 / 512.0)).r;

        vec3 spherenormal = v_Normal;
        vec3 b_nor = normalize(vec3(b_center - b_right, b_center - b_top, 0.2));
        b_nor = eastNorthUpToEyeCoordinates(v_Position, spherenormal) * b_nor;
        // surface normal - normalized after rasterization
        vec3 surfnormal = normalize(b_nor);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);

        float diffuse = clamp(dot(u_CameraSpaceDirLight, spherenormal), 0.0, 1.0);
        float surfDiff = clamp(dot(u_CameraSpaceDirLight, surfnormal), 0.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, surfnormal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        // Gamma correction
        dayColor = pow(dayColor, vec3(1.0 / 1.4));
        float specularity = texture2D(u_EarthSpec, v_Texcoord).r;
        dayColor *= (0.8 * surfDiff) + (0.4 * specular * specularity);

        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
        // Gamma correction
        nightColor = pow(nightColor, vec3(1.0 / 1.2));
        float blendfactor = pow(diffuse, 0.7);

        vec3 surfaceColor = mix(nightColor, dayColor, blendfactor);

        vec3 cloud = texture2D(u_Cloud, cloudTC).rgb;

        vec3 color = mix(cloud * diffuse, surfaceColor, cloudTrans);

        float rim = max(0.0, 1.5 + dot(v_Normal, v_Position));
        vec3 rimcolor = vec3(0.2, 0.3, 0.4) * rim;
        color += rimcolor * (blendfactor + 0.3);

        gl_FragColor = vec4(color, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
</script>

<script src ="js/lib/gl-matrix.js" type ="text/javascript"></script>
<script src ="js/webGLUtility.js" type ="text/javascript"></script>
<script src ="js/frag_globe.js" type ="text/javascript"></script>
</body>

</html>
